+++
title = "Clojureのderiveとmultimethod"
author = ["karuta"]
date = 2021-07-17T19:43:00+09:00
publishDate = 2021-07-17T00:00:00+09:00
tags = ["clojure"]
draft = false
+++

`derive` は clojureのデータ同士に親子関係を設定することができる関数である。そしてこの機能は `multimethod` のディスパッチに利用することができる。  

<!--more-->  

まず `::` について少し説明。これをつかえば名無しの `:child` というキーワードではなく、ファイルの名前空間上(今はuser)のキーワードであること追加してくれる。  

```clojure
::child
;; => :user/child
```

`derive` の第一引数に子を、第二引数に親を指定して、親を定義する  

```clojure
(derive ::child ::parent)
```

祖父母もつくってみる。  

```clojure
(derive ::parent ::grand-parent)
```

親子関係を取得する  

```clojure
(parents ::child)
;; => #{:user/parent}

(ancestors ::child)
;; => #{:user/parent :user/grand-parent}

(descendants ::grand-parent)
;; => #{:user/parent :user/child}          
```

親子関係のチェックには `isa?` 関数をつかう。第一引数に子を、第二引数に親を指定しないと動作しない。  

```clojure
(isa? ::child ::parent)
;; => true
(isa? ::parent ::child)
;; => false
```


## multimethodと組み合わせる {#multimethodと組み合わせる}

たとえば、 mapに該当のキーがないや、不正な値が送られた来た場合を、それぞれ `::null-key`, `::invalid-value` としてエラー定義する。そして、どちらもvalidationエラーではあるので、共通の親として `validation-error` を定義すると次のようになる。  

```clojure
(derive ::null-key ::validation-error)
(derive ::invalid-value ::validation-error)
```

次に `multimehod` をつかって、タイプで分岐しようと考える。個々のエラーではなく親である `validation-error` をつかうと、 `null-key, invalid-value` のどちらがきてもバリデーションエラーという文字列を返してくれる。  

```clojure
(defmulti validate :type)
(defmethod validate ::ok [d] (dissoc d :type))
(defmethod validate ::validation-error [_] "バリデーションエラー")

(validate {:type ::null-key})
;; => "バリデーションエラー"

(validate {:type ::invalid-value})
;; => "バリデーションエラー"

(validate {:type ::ok :id 1 :name "hoge"})
;; => {:id 1, :name "hoge"}
```
