+++
title = "Clojure.specのエラーを表示したい"
author = ["karuta"]
date = 2021-07-18T12:30:00+09:00
publishDate = 2021-07-18T00:00:00+09:00
tags = ["clojure"]
draft = false
+++

clojure.specをつかって、validationの実装について考えてみる。  
さらにvalidationの結果がNGのときに、どうやって人間が読める内容に変換できるかということも考える  

<!--more-->  

## specとvaliDation {#specとvalidation}

まずspecをimportと定義をしていこう  

```clojure
(require '[clojure.spec.alpha :as s])
(s/def ::name string?)
(s/def ::age int?)
(s/def ::person (s/keys :req-un [::name ::age]))
```

次に `:pre` で事前条件を記述する。ここでは引数が `::person` を満たさないとエラーを吐くようにしてる。  

```clojure
(defn call-name [p]
  {:pre [(s/valid? ::person p)]}
  (get p :name))

;; 事前条件に問題なければ、値が取得できる
(call-name {:name "adam" :age 10})
;; => "adam"
```

事前条件をみたなさないとどうなるかというと、 次の通り `AssertionError` が発生する  

```clojure
(call-name {:name "adam" :age "10"})
;;  class java.lang.AssertionErrorclass java.lang.AssertionErrorExecution error (AssertionError) at user/call-name (REPL:1).
;; : Assert failed: (s/valid? :user/person p)
```

`try/catch` で捕まえて、内容を表示すると次の通り。  

```clojure
(try
  (call-name {:name "adam" :age "10"})
  (catch java.lang.AssertionError e
    (print e)))
```

しかし、だめなことはわかるが、なぜだめなのかがわからないのが問題である。  

```text
#error {
 :cause Assert failed: (s/valid? :user/person p)
 :via
 [{:type java.lang.AssertionError
   :message Assert failed: (s/valid? :user/person p)
   :at [user$call_name invokeStatic NO_SOURCE_FILE 1]}]
 :trace
 [[user$call_name invokeStatic NO_SOURCE_FILE 1]
  [user$call_name invoke NO_SOURCE_FILE 1]
  [user$eval7575 invokeStatic NO_SOURCE_FILE 2]
  [user$eval7575 invoke NO_SOURCE_FILE 1]
  [clojure.lang.Compiler eval Compiler.java 7181]
  [clojure.lang.Compiler eval Compiler.java 7136]
  [clojure.core$eval invokeStatic core.clj 3202]
  [clojure.core$eval invoke core.clj 3198]
  [nrepl.middleware.interruptible_eval$evaluate$fn__963$fn__964 invoke interruptible_eval.clj 87]
  [clojure.lang.AFn applyToHelper AFn.java 152]
  [clojure.lang.AFn applyTo AFn.java 144]
  [clojure.core$apply invokeStatic core.clj 667]
  [clojure.core$with_bindings_STAR_ invokeStatic core.clj 1977]
  [clojure.core$with_bindings_STAR_ doInvoke core.clj 1977]
  [clojure.lang.RestFn invoke RestFn.java 425]
  [nrepl.middleware.interruptible_eval$evaluate$fn__963 invoke interruptible_eval.clj 87]
  [clojure.main$repl$read_eval_print__9110$fn__9113 invoke main.clj 437]
  [clojure.main$repl$read_eval_print__9110 invoke main.clj 437]
  [clojure.main$repl$fn__9119 invoke main.clj 458]
  [clojure.main$repl invokeStatic main.clj 458]
  [clojure.main$repl doInvoke main.clj 368]
  [clojure.lang.RestFn invoke RestFn.java 1523]
  [nrepl.middleware.interruptible_eval$evaluate invokeStatic interruptible_eval.clj 84]
  [nrepl.middleware.interruptible_eval$evaluate invoke interruptible_eval.clj 56]
  [nrepl.middleware.interruptible_eval$interruptible_eval$fn__994$fn__998 invoke interruptible_eval.clj 152]
  [clojure.lang.AFn run AFn.java 22]
  [nrepl.middleware.session$session_exec$main_loop__1062$fn__1066 invoke session.clj 202]
  [nrepl.middleware.session$session_exec$main_loop__1062 invoke session.clj 201]
  [clojure.lang.AFn run AFn.java 22]
  [java.lang.Thread run Thread.java 829]]}
```


## なぜだめだったのかをわかるようにする {#なぜだめだったのかをわかるようにする}

なぜ事前条件をみたさなかったのかを得るには2つのやりかたがある。  

1つめは、 `s/valid?` ではなく、 `s/assert` を使うやり方である。まず `(s/check-asserts true)` を実行してから、さきほどと同じようにやってみよう。  

```clojure
(defn call-name [p]
  {:pre [(s/assert ::person p)]}
  (get p :name))

(s/check-asserts true)
```

```clojure
(try
  (call-name {:name "adam" :age "10"})
  (catch clojure.lang.ExceptionInfo e
    (ex-data e)))   
;; => #:clojure.spec.alpha{:problems ({:path [:age], 
;;                                     :pred clojure.core/int?, 
;;                                     :val "10", 
;;                                     :via [:user/age], 
;;                                     :in [:age]}), 
;;                         :spec :user/person, 
;;                         :value {:name "adam", :age "10"}, 
;;                         :failure :assertion-failed}
```

これで意味ある情報がえられた。 `:via` を見ると、 `:user/age` でだめであったことが確認できる。  

つぎは、 独自の述語関数をつくることである。ここでは `check` 関数をつくって、条件を満たさない場合 `clojure.lang.ExceptionInfo` を投げるようにしてる。  

```clojure
(defn check [spec data]
  (if (s/valid? spec data)
    true
    (throw (ex-info "Assertion" (s/explain-data spec data)))))

(defn call-name [p]
  {:pre [(check ::person p)]}
  (get p :name))   
```

```clojure
(try
  (call-name {:name "adam" :age "10"})
  (catch clojure.lang.ExceptionInfo e
    (ex-data e)))
;; => #:clojure.spec.alpha{:problems ({:path [:age], 
;;                                     :pred clojure.core/int?, 
;;                                     :val "10", 
;;                                     :via [:user/person :user/age], 
;;                                     :in [:age]}), 
;;                         :spec :user/person, 
;;                         :value {:name "adam", :age "10"}}
```

ここでは `:in` から何がだめなのかがわかる。どうして違うのかが不明だが、また今度にしよう。  

これで何が原因で事前条件が満たせなかったのかを得られるようになった。つぎにこれをより人間がわかるようなエラーに変換する方法を検討しよう  


## エラーを日本語に変換する方法について考えてみる {#エラーを日本語に変換する方法について考えてみる}

ポイントは、 `:via` を利用することである。まずここだけを取得できるようにしてみよう。  

```clojure
(try
  (call-name {:name "adam" :age "10"})
  (catch clojure.lang.ExceptionInfo e
    (-> 
      (ex-data e)
      (get :clojure.spec.alpha/problems)
      (first)
      (get :via)
      )))    
;; => [:user/age]
```

つぎに、 取得したキーとエラーメッセージを紐付ける関数をつくる  

```clojure
(defn get-message
  [via messages]
  (->> (reverse via)
      (some messages)))
```

```clojure
(try
  (call-name {:name "adam" :age "10"})
  (catch clojure.lang.ExceptionInfo e
    (-> 
      (ex-data e)
      (get :clojure.spec.alpha/problems)
      (first)
      (get :via)
      ((fn [c] (get-message c {::name "名前がおかしいよ" ::age "年齢おかしいよ"}))))))
;; => "年齢おかしいよ"
```

いけましたね。でも、これはエラーが複数存在しても、複数エラーメッセージを表示してくれない。ということで名前がだめなときも表示できるように改良しよう。  

エラー抽出関数をつぎのように定義する。  
mapしてからnilのものをfilterで省いてる。  

```clojure
(defn get-messages
  [via messages]
  (->> 
    (reverse via)
    (map messages)
    (filter seq)))
```

つぎに、 `:via` も複数取れるように変更にする  

```clojure
  (try
    (call-name {:name 1 :age "10"})
    (catch clojure.lang.ExceptionInfo e
      (-> 
      (ex-data e)
      (get :clojure.spec.alpha/problems)
      ((fn [p] (flatten (map #(get % :via) p))))
      )))
;; => (:user/name :user/age)
```

即時関数をより分割してつぎのようにもかける  

```clojure
  (try
    (call-name {:name 1 :age "10"})
    (catch clojure.lang.ExceptionInfo e
      (-> 
      (ex-data e)
      (get :clojure.spec.alpha/problems)
      ((fn [c] (map #(get % :via) c)))
      (flatten)
      )))
;; => (:user/name :user/age)
```

で、最後に `get-messages` を使うと複数のエラーメッセージを取得できる  

```clojure
(try
  (call-name {:name 1 :age "10"})
  (catch clojure.lang.ExceptionInfo e
    (-> 
      (ex-data e)
      (get :clojure.spec.alpha/problems)
      ((fn [p] (flatten (map #(get % :via) p))))
      (get-messages {::name "名前がおかしいよ" ::age "年齢おかしいよ"}))))
;; ("年齢おかしいよ" "名前がおかしいよ")
```


## 感想 {#感想}

以上がclojure.specでエラーメッセージを表示するやり方である。正直使いにくいというのが思うところだ。  

日本語に変換が必要な場合は、 `malli` を使ったほうが良さそう。  
clojure.specの使いみちを考えないと。  


## 参考URL {#参考url}

<https://clojure.wladyka.eu/posts/form-validation/>  

