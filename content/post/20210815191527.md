+++
title = "clojureのテストでspyを利用する"
author = ["karuta"]
date = 2021-08-15T19:15:00+09:00
tags = ["clojure"]
draft = false
+++

以前にclojureでmockする場合 `with-redefs` を使うことを紹介したが、もう少し厳密に言うと、これはモックというよりスタブする方法である。スタブは返り値を任意に値に変換できるものである。このスタブに渡された引数が正しいかどうかを確認したい場合どうするか？それがスパイである。今回はスパイをClojureで使う方法を整理する。  

<!--more-->  

スパイする前にやりたいことをもう少しちゃんと説明しておく。ある関数(f)が、別の関数(g)に依存していたとき、fをテストするときにgはスタブにして任意の値を出力するように変えたい。さらにg関数に対して適切な引数がわたされてるかであったり、呼び出されているかも確認したい。  

f,gがそれぞれ異なる名前空間にあったとき、この要件を実現する方法を考える。  

spyライブラリとして、 `tortue/spy` を使用する  

まずはg関数となる、sqaure関数をsample-spy.exportという名前空間に作成するa  

```clojure
(ns sample-spy.export)

(defn square
 [x]
 (* x x))
```

次にこのsquareを利用する関数distanceを別の名前空間に作成する。これがfになる。  

```clojure
(ns sample-spy.core
  (:require
   [sample-spy.export :as e]))


(defn distance
  [x y]
  (+ (e/square x) (e/square y)))
```

`:require` からわかるようにdistance関数は、square関数に依存してる。やりたいことは、このsquare関数をスタブにもスパイにもしたいということである。  

```clojure
(ns sample-spy.core-test
  (:require
    [clojure.test :refer :all]
    [sample-spy.core :as c]
    [sample-spy.export :as e]
    [spy.core :as spy]))   

(deftest spy-test
  (testing "スタブかつスパイのテスト"
    (with-redefs [e/square (spy/spy (constantly 1))]
      (is (= 2 (c/distance 1 2) "1 + 1 = 2である"))
      (is (= [[1] [2]] (spy/calls e/square)) "一回目の引数は1であり、二回目は2である")
      (is (spy/called-n-times? e/square 2) "呼び出し回数は2回である"))))
```

スタブ化するには、 `with-redefs` をつかう。そしてスパイにするには、 差し替える関数をさらに `(spy/spy)` でラップする。まずはじめのテストからスタブが成功してることがわかる。つぎのテストでは、 `spy/calls` によって引数が意図するものかや、 `spy/called-n-times?` で呼び出し回数を確認できる
